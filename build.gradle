plugins {
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'com.google.cloud.tools.jib' version '3.2.1'
}

def buildPhase = System.getProperty("build.phase", "local")
project.ext.buildPhase = buildPhase

def encryptorPassword = System.getProperty("encryptorPassword", "")
project.ext.encryptorPassword = encryptorPassword

apply from: "jib.gradle"

configurations {
    asciidoctorExtensions
}

ext {
    snippetsDir = file('build/generated-snippets')
    set('springCloudVersion', "2021.0.1")
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
        copy {
            from "${asciidoctor.outputDir}"
            into 'src/main/resources/static/docs'
        }
    }
}

group = 'com.comeet'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "mysql:mysql-connector-java:$mysqlVersion"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    /* 로그인 관련 */
    implementation 'org.springframework.boot:spring-boot-starter-security'

    /* jwt 관련 */
    implementation "com.auth0:java-jwt:$javaJwtVersion"

    /* model validation 관련 */
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    /* github commit 관련 */
    implementation "org.kohsuke:github-api:$githubApiVersion"

    /* FeignClient 관련 */
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"

    /* json parse 관련 */
    implementation "com.googlecode.json-simple:json-simple:$jsonSimpleVersion"

    /* jasypt 관련 */
    implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:$jasyptVersion"

    /* Rest Docs 관련 */
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    /* Crawling 관련 */
    implementation 'org.jsoup:jsoup:1.13.1'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
